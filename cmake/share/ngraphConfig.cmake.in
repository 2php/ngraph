# ******************************************************************************
# Copyright 2017-2019 Intel Corporation
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ******************************************************************************
#
#
# FindNGraph
# ------
#
# This will define the following variables:
#
#   ngraph_FOUND        - True if the system has the nGraph library
#   NGRAPH_LIBRARIES    - nGraph libraries
#   ngraph::ngraph      - nGraph core target
#
#

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(Threads)

if(NOT TARGET ngraph)
    include("${CMAKE_CURRENT_LIST_DIR}/ngraphTargets.cmake")
else()
    if (NOT ("${CMAKE_GENERATOR}" STREQUAL "Ninja"))
        add_library(ngraph::ngraph SHARED IMPORTED)
        add_dependencies(ngraph::ngraph ngraph)
        set_target_properties(ngraph::ngraph PROPERTIES
            INTERFACE_COMPILE_DEFINITIONS $<TARGET_PROPERTY:ngraph,INTERFACE_COMPILE_DEFINITIONS>
            INTERFACE_INCLUDE_DIRECTORIES $<TARGET_PROPERTY:ngraph,INTERFACE_INCLUDE_DIRECTORIES>
            INTERFACE_LINK_LIBRARIES $<TARGET_PROPERTY:ngraph,INTERFACE_LINK_LIBRARIES>
            )
        get_target_property(_LIB_NAME ngraph NAME)
        get_target_property(_LIB_LOC ngraph LIBRARY_OUTPUT_DIRECTORY)
        if(CMAKE_BUILD_TYPE STREQUAL Release)
            set(_LIB_POSTFIX ${CMAKE_RELEASE_POSTFIX})
        else()
            set(_LIB_POSTFIX ${CMAKE_DEBUG_POSTFIX})
        endif()

        set_target_properties(ngraph::ngraph PROPERTIES
            IMPORTED_LOCATION ${_LIB_LOC}/${CMAKE_SHARED_LIBRARY_PREFIX}${_LIB_NAME}${_LIB_POSTFIX}${CMAKE_SHARED_LIBRARY_SUFFIX}
            )
	endif()
endif()

set(NGRAPH_LIBRARIES ngraph::ngraph)
check_required_components(ngraph)
